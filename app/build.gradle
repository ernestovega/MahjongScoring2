apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "es.etologic.mahjongscoring2"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "MahjongScoring2"
    productFlavors {
        mock {
            applicationIdSuffix ".Mock"
            versionNameSuffix '-Mock'
            buildConfigField "String", "PACKAGE_NAME", "\"com.mahjongscoring.activities\""
        }
        dev {
            applicationIdSuffix ".Dev"
            versionNameSuffix '-Dev'
            buildConfigField "String", "PACKAGE_NAME", "\"com.mahjongscoring.activities\""
        }
        pro {
            buildConfigField "String", "PACKAGE_NAME", "\"es.etologic.mahjongscoring2\""
        }
    }
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release' && (variant.getFlavors().get(0).name == 'mock' ||
                variant.getFlavors().get(0).name == "dev")) variant.setIgnore(true)
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'

    //ARCH COMPONENTS
    implementation "android.arch.persistence.room:runtime:$archComponentsPersistanceVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$archComponentsPersistanceVersion"
    implementation "android.arch.lifecycle:runtime:$archComponentsLifecycleVersion"
    implementation "android.arch.lifecycle:extensions:$archComponentsLifecycleVersion"
    annotationProcessor "android.arch.lifecycle:common-java8:$archComponentsLifecycleVersion"
    testImplementation("android.arch.core:core-testing:$archComponentsPersistanceVersion", {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
        exclude group: 'com.android.support', module: 'appcompat-v7'
    })

    //SUPPORT & DESIGN
    implementation "com.android.support:appcompat-v7:$supportLibrariesVersion"
    implementation "com.android.support:customtabs:$supportLibrariesVersion"
    implementation "com.android.support:design:$supportLibrariesVersion"
    implementation "com.android.support:support-v4:$supportLibrariesVersion"
    implementation "com.android.support:support-compat:$supportLibrariesVersion"
    implementation "com.android.support:cardview-v7:$supportLibrariesVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibrariesVersion"

    //DAGGER
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"

    //BUTTERKNIFE
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    //RX BINDINGS
    implementation 'com.jakewharton.rxbinding:rxbinding:0.4.0'

    //RX JAVA
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.0'

    //UTILS
    implementation "org.apache.commons:commons-lang3:3.6"

    //CHIPS
    implementation "com.github.pchmn:MaterialChipsInput:1.0.8"

    //FAB MENU
    implementation 'com.github.clans:fab:1.6.4'
}