apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        applicationId "es.etologic.mahjongscoring2"
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "MahjongScoring2"
    productFlavors {
        mock {
            applicationIdSuffix ".Mock"
            versionNameSuffix '-Mock'
        }
        dev {
            applicationIdSuffix ".Dev"
            versionNameSuffix '-Dev'
        }
        pro {}
    }
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release' &&
                (variant.getFlavors().get(0).name == 'mock' ||
                        variant.getFlavors().get(0).name == "dev")) {
            variant.setIgnore(true)
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {

    //Design
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"

    //View bindings
    compile "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    //Architecture components & Rooms
    compile "android.arch.persistence.room:runtime:$archComponentsVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$archComponentsVersion"
    compile "android.arch.lifecycle:runtime:$archComponentsVersion"
    compile "android.arch.lifecycle:extensions:$archComponentsVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$archComponentsVersion"

    //Test
    testCompile("android.arch.core:core-testing:$archComponentsVersion", {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    })
    testImplementation "junit:junit:4.12"
    androidTestImplementation "com.android.support.test:runner:1.0.1"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.1"
    testCompile "org.mockito:mockito-core:2.12.0"

}